Root <- Skip Comment? Rule (EOL Comment? Packet?)* EOF

Rule <- Skip Packet (SEPT Packet)* COLON Skip Action (SEPT Action)*

bin     <- [01]
bin_    <- '_'? bin
oct     <- [0-7]
oct_    <- '_'? oct
hex     <- [0-9a-fA-F]
hex_    <- '_'? hex
dec     <- [0-9]
dec_    <- '_'? dec

bin_int <- bin bin_*
oct_int <- oct oct_*
dec_int <- dec dec_*
hex_int <- hex hex_*

INTEGER <- "0b" bin_int Skip / "0o" oct_int Skip
            / "0x" hex_int Skip / dec_int   Skip

EQUAL           <- '=' Skip
COLON           <- ':'
COMMA           <- ',' Skip
SLASH           <- '/' Skip
DOT             <- '.'
OR              <- '|' Skip
Open            <- '(' Skip
Close           <- ')' Skip
Skip            <- ( Space / Comment )*
Comment         <- '#' ( !EOL . )* EOL
Space           <- ' ' / '\t' / EOL
EOL             <- '\r\n' / '\n' / '\r' / '\0'
EOF             <- !.

Action          <- 'RSS'
                   / 'DROP'
                   / 'QUEUE'
                   / 'PORT'
                   / 'METER'
                   / 'SAMPLE'  { YY_BPF(yy, (BPF_K|BPF_RET), 0, 0, 0x40000); YY_BPF(yy, BPF_K|BPF_RET, 0, 0, 0); }

SEPT            <- SLASH Skip / OR

Packet          <- ETHER_P / IP6_P / IP_P / UDP_P / TCP_P / VXLAN_P / GRE_P / VLAN_P / MPLS_P / ANY_P / SCTP_P / ICMP6_P / ICMP_P / ARP_P

tcp_flags       <- 'syn' / 'fin' / 'rst' / 'ack' / 'push' / 'ece' / 'cwr' / 'urg'

ETHER_P         <- 'ETHER' (Open (
                    ('dst' EQUAL <macaddr (SLASH macaddr)?> COMMA?)         { YY_ETHER_MAC(yy, yytext, false); }
                    / ('src' EQUAL <macaddr (SLASH macaddr)?> COMMA?)       { YY_ETHER_MAC(yy, yytext, true); }
                    / ('type' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)      { YY_ETHER_TYPE(yy, yytext);}
                    )* Close)?                                              { yy->offset += 14; }
IP_P            <- 'IP' (Open (
                    ('src' EQUAL <ipaddr (SLASH (ipaddr / ipprefix))?> COMMA?)   { YY_IP_ADDR(yy, yytext, true); }
                    / ('dst' EQUAL <ipaddr (SLASH (ipaddr / ipprefix))?> COMMA?) { YY_IP_ADDR(yy, yytext, false); }
                    / ('tos' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)          { YY_IP_FIELD(yy, yytext, 1, 1); }
                    / ('len' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)          { YY_IP_FIELD(yy, yytext, 2, 2); }
                    / ('id' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)           { YY_IP_FIELD(yy, yytext, 4, 2); }
                    / ('frag' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)         { YY_IP_FIELD(yy, yytext, 6, 2); }
                    / ('ttl' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)          { YY_IP_FIELD(yy, yytext, 8, 1); }
                    / ('proto' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)        { YY_IP_FIELD(yy, yytext, 9, 1); }
                    / ('csum' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)         { YY_IP_FIELD(yy, yytext, 10, 2); }
                    )* Close)?                                              { yy->offset += 20; }
VXLAN_P         <- 'VXLAN' (Open (
                    ('flags' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)           { YY_VXLAN_FLAG(yy, yytext); }
                    / ('vni' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)           { YY_VXLAN_VNI(yy, yytext); }
                    )* Close)?                                              { yy->offset += 8; }
UDP_P           <- 'UDP' (Open (
                    ('src' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)             { YY_UDP_FIELD(yy, yytext, 0); }
                    / ('dst' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)           { YY_UDP_FIELD(yy, yytext, 2); }
                    / ('len' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)           { YY_UDP_FIELD(yy, yytext, 4); }
                    / ('csum' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)          { YY_UDP_FIELD(yy, yytext, 6); }
                    )* Close)?                                              { yy->offset += 8; }
TCP_P           <- 'TCP' (Open (
                    ('src' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)            { YY_TCP_FIELD(yy, yytext, 0, 2); }
                    / ('dst' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)          { YY_TCP_FIELD(yy, yytext, 2, 2); }
                    / ('seq' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)          { YY_TCP_FIELD(yy, yytext, 4, 4); }
                    / ('ackseq' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)       { YY_TCP_FIELD(yy, yytext, 8, 4); }
                    / ('doff' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)         { YY_TCP_FIELD(yy, yytext, 12, 1); }
                    / ('win' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)          { YY_TCP_FIELD(yy, yytext, 14, 2); }
                    / ('csum' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)         { YY_TCP_FIELD(yy, yytext, 16, 2); }
                    / ('urgptr' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)       { YY_TCP_FIELD(yy, yytext, 18, 2); }
                    / ('flags' EQUAL <tcp_flags (SEPT tcp_flags)*>)            { YY_TCP_FLAGS(yy, yytext); }
                    )* Close)?                                              { yy->offset += 20; }
VLAN_P          <- 'VLAN' (Open (
                    'tpid' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?               { YY_VLAN_TPID(yy, yytext); }
                    / 'tag' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?              { YY_VLAN_TAG(yy, yytext); }
                    )* Close)?                                              { yy->offset += 4; }
MPLS_P          <- 'MPLS' (Open (
                    ('label' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)
                    / ('ttl' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)
                    )* Close)?
GRE_P           <- 'GRE' (Open (
                    ('proto' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)           {}
                    / ('csum' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)          {}
                    / ('key' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)           {}
                    / ('recur' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)         {}
                    )* Close)?                                          { yy->offset += 4; }
IP6_P           <- "IP6" (Open (
                    ('src' EQUAL <ip6addr (SLASH ip6prefix)?> COMMA?)             { YY_IP6_ADDR(yy, yytext, true);}
                    / ('dst' EQUAL <ip6addr (SLASH ip6prefix)?> COMMA?)           { YY_IP6_ADDR(yy, yytext, false);}
                    / ('version' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)        { YY_IP6_FIELD(yy, yytext, "version"); }
                    / ('traffic_class' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)  { YY_IP6_FIELD(yy, yytext, "traffic_class"); }
                    / ('flowlabel' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)      { YY_IP6_FIELD(yy, yytext, "flowlabel"); }
                    / ('plen' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)           { YY_IP6_FIELD(yy, yytext, "plen"); }
                    / ('nexthdr' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)        { YY_IP6_FIELD(yy, yytext, "nexthdr"); }
                    / ('hoplimit' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)       { YY_IP6_FIELD(yy, yytext, "hoplimit"); }
                    )* Close)?                                          { yy->offset += 40; }
SCTP_P          <- "SCTP" (Open (
                    ('src' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)         { YY_SCTP_FIELD(yy, yytext, 0, 2);}
                    / ('dst' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)       { YY_SCTP_FIELD(yy, yytext, 2, 4);}
                    / ('tag' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)       { YY_SCTP_FIELD(yy, yytext, 4, 4);}
                    / ('csum' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)      { YY_SCTP_FIELD(yy, yytext, 8, 4);}
                    / ('type' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)
                    / ('flag' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)
                    / ('len' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)
                    )* Close)?                                                  { yy->offset += 12; }
ICMP_P          <- "ICMP" (Open (
                    ('type' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)                    { YY_ICMP_FIELD(yy, yytext, 0, 1); }
                    / ('code'  EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)                 { YY_ICMP_FIELD(yy, yytext, 1, 1); }
                    / ('csum'  EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)                 { YY_ICMP_FIELD(yy, yytext, 2, 2); }
                    / ('id' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)                    { YY_ICMP_FIELD(yy, yytext, 4, 2); }
                    / ('seq' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)                   { YY_ICMP_FIELD(yy, yytext, 6, 2); }
                    )* Close)?                                                 { yy->offset += 8; }
ARP_P           <- "ARP" (Open (
                    ('sha' EQUAL <ipaddr (SLASH (ipaddr / ipprefix))?> COMMA?)             { YY_ARP_ADDR(yy, yytext, 8); }
                    / ('spa' EQUAL <ipaddr (SLASH (ipaddr / ipprefix))?> COMMA?)           { YY_ARP_ADDR(yy, yytext, 12); }
                    / ('tha' EQUAL <ipaddr (SLASH (ipaddr / ipprefix))?> COMMA?)           { YY_ARP_ADDR(yy, yytext, 16); }
                    / ('tpa' EQUAL <ipaddr (SLASH (ipaddr / ipprefix))?> COMMA?)           { YY_ARP_ADDR(yy, yytext, 20); }
                    / ("hrd" EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)                      { YY_ARP_FIELD(yy, yytext, 0, 2); }
                    / ("pro" EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)                      { YY_ARP_FIELD(yy, yytext, 2, 2); }
                    / ("hlen" EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)                     { YY_ARP_FIELD(yy, yytext, 4, 1); }
                    / ("plen" EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)                     { YY_ARP_FIELD(yy, yytext, 5, 1); }
                    / ("opcode" EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)                   { YY_ARP_FIELD(yy, yytext, 6, 2); }
                    )* Close)?                                                 { yy->offset += 24; }
ICMP6_P         <- "ICMP6" (Open (
                    ('type' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)
                    / ('code' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)
                    / ('csum' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)
                    )* Close)?
ANY_P           <- "ANY" (Open (
                    ('off' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)
                    / ('size' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)
                    / ('value' EQUAL <INTEGER (SLASH INTEGER)?> COMMA?)
                    )* Close)?

macaddr         <- hex hex COLON hex hex COLON hex hex COLON hex hex COLON hex hex COLON hex hex Skip
ipaddr          <- ip4seg DOT ip4seg DOT ip4seg DOT ip4seg Skip
ip4seg          <- '25'[0-5] / '2'[0-4][0-9] / '1'[0-9][0-9] / [1-9][0-9] / [0-9]
ipprefix        <- ('3'[0-2] / [1-2][0-9] / [0-9]) Skip
group           <- hex (hex (hex hex?)?)?
ip6addr         <- (group COLON group COLON group COLON group COLON group COLON group COLON group COLON group Skip)
                    / (COLON COLON group (COLON group (COLON group (COLON group (COLON group (COLON group (COLON group)?)?)?)?)?)? Skip)
                    / (group COLON COLON group (COLON group (COLON group (COLON group (COLON group (COLON group)?)?)?)?)? Skip)
                    / (group (COLON group)? COLON COLON group (COLON group (COLON group (COLON group (COLON group)?)?)?)? Skip)
                    / (group (COLON group (COLON group)?)? COLON COLON group (COLON group (COLON group (COLON group)?)?)? Skip)
                    / (group (COLON group (COLON group (COLON group)?)?)? COLON COLON group (COLON group (COLON group)?)? Skip)
                    / (group (COLON group (COLON group (COLON group (COLON group)?)?)?)? COLON COLON group (COLON group)? Skip)
                    / (group (COLON group (COLON group (COLON group (COLON group (COLON group)?)?)?)?)? COLON COLON group Skip)
                    / (group (COLON group (COLON group (COLON group (COLON group (COLON group (COLON group)?)?)?)?)?)? COLON COLON Skip)
ip6prefix       <- ('12'[0-8] / '1'[0-1][0-9] / [1-9][0-9] / [0-9]) Skip